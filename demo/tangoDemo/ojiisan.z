// NPC variant. Uses a unique string and message style. Gives Link a sword.

ffc script Ojiisan
{
    void run()
    {
        bool talking=false;
        int slot;
        
        int jpn[]= { KNJ_ICHI, KNJ_JIN, HG_DE, HG_HA, KNJ_KI, KNJ_KEN, HG_JI, HG_SMALL_YA, KUTEN,
                     HG_KO, HG_RE, HG_WO, KNJ_JU, HG_KE, HG_YO, HG_U, KUTEN, NULL };
        int format[]="%s@sync(1)";
        int string[26];
        sprintf(string, format, jpn);
        
        while(true)
        {
            if(!talking && !linkAlreadyTalking && LinkWantsToTalk(this))
            {
                slot=ShowString(string, TANGO_SLOT_NORMAL, STYLE_JAPANESE, 32, 32);
                if(slot==TANGO_INVALID)
                    talking=false;
                else
                {
                    talking=true;
                    linkAlreadyTalking=true;
                    Tango_Sync(1);
                    item sword=Screen->CreateItem(5);
                    sword->X=Link->X;
                    sword->Y=Link->Y;
                }
            }
            
            if(talking)
            {
                talking=Tango_SlotIsActive(slot);
                if(!talking)
                    linkAlreadyTalking=false;
            }
            
            Waitframe();
        }
    }
}
