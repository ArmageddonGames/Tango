// NPC variant. Uses a unique string and message style. Gives Link a sword.

ffc script Ojiisan
{
    void run()
    {
        bool talking=false;
        int stringID;
        
        int string[]= { KNJ_ICHI, KNJ_JIN, HG_DE, HG_HA, KNJ_KI, KNJ_KEN, HG_JI, HG_SMALL_YA, KUTEN,
                        HG_KO, HG_RE, HG_WO, KNJ_JU, HG_KE, HG_YO, HG_U, KUTEN, NULL };
        
        while(true)
        {
            if(!talking && !linkAlreadyTalking && LinkWantsToTalk(this))
            {
                stringID=ShowString(string, TANGO_STRING_NORMAL, STYLE_JAPANESE, 32, 32);
                if(stringID==TANGO_INVALID)
                    talking=false;
                else
                {
                    talking=true;
                    linkAlreadyTalking=true;
                    Waitframes(20);
                    item sword=Screen->CreateItem(5);
                    sword->X=Link->X;
                    sword->Y=Link->Y;
                }
            }
            
            if(talking)
            {
                talking=Tango_StringIsVisible(stringID);
                if(!talking)
                    linkAlreadyTalking=false;
            }
            
            Waitframe();
        }
    }
}
