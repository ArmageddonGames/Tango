// Juust a few functions common to the different scripts.

bool linkAlreadyTalking;

int ShowString(int string, int type, int style, float x, float y)
{
    int stringID=Tango_GetFreeString(type);
    if(stringID==TANGO_INVALID)
        return stringID;
    
    Tango_ClearString(stringID);
    Tango_SetString(stringID, string);
    Tango_SetStringStyle(stringID, style);
    Tango_SetStringPosition(stringID, x, y);
    Tango_ActivateString(stringID);
    
    return stringID;
}

int ShowMessage(int message, int type, int style, float x, float y)
{
    int stringID=Tango_GetFreeString(type);
    if(stringID==TANGO_INVALID)
        return stringID;
    
    Tango_ClearString(stringID);
    Tango_SetMessage(stringID, message);
    Tango_SetStringStyle(stringID, style);
    Tango_SetStringPosition(stringID, x, y);
    Tango_ActivateString(stringID);
    
    return stringID;
}

// Used by NPCs.
bool LinkWantsToTalk(ffc this)
{
    if(!Link->PressA)
        return false;
    
    if(Link->Dir==DIR_UP)
    {
        if(Abs(Link->X-this->X)<8 && Abs(Link->Y-8-this->Y)<8)
            return true;
    }
    else if(Link->Dir==DIR_DOWN)
    {
        if(Abs(Link->X-this->X)<8 && Abs(Link->Y+16-this->Y)<8)
            return true;
    }
    else if(Link->Dir==DIR_LEFT)
    {
        if(Abs(Link->X-16-this->X)<8 && Abs(Link->Y-this->Y)<8)
            return true;
    }
    else
    {
        if(Abs(Link->X+16-this->X)<8 && Abs(Link->Y-this->Y)<8)
            return true;
    }
    
    return false;
}
